# This is an example PKGBUILD file. Use this as a start to creating your own,
# and remove these comments. For more information, see 'man PKGBUILD'.
# NOTE: Please fill out the license field for your package! If it is unknown,
# then please put 'unknown'.

# Maintainer: Sophonie Bouye <sophoniebouye@gmail.com>
# Version notes:
# `curl https://brave-browser-downloads.s3.brave.com/latest/release.version`
pkgname=brave-bin
pkgver=1.45.123
pkgrel=1
epoch=1
pkgdesc="A web browser that stops ads and trackers by default  (binary release)"
arch=(x86_64)
url="https://www.brave.com/download"
license=('custom:chromium')
depends=(alsa-lib
         gtk3
         json-glib
         libva
         libxss
         nss
         ttf-font)
optdepends=('cups: Printer support'
            'libgnome-keyring: Enable GNOME keyring support'
            'libnotify: Native notification support')
provides=("${pkgname%-bin}=$pkgver" 'brave-browser')
conflicts=("${pkgname%-bin}")
options=(!strip)
source=("$pkgname-$pkgver.zip::https://github.com/brave/brave-browser/releases/download/v$pkgver/brave-browser-$pkgver-linux-amd64.zip"
	"$pkgname.sh"
	'brave-browser.desktop')
noextract=("$pkgname-$pkgver.zip")
sha256sums=('73cf72418cbc019df2082079fbd28f760ef903050309d27a77992aac9cf76feb'
            'ba7d57a3328c68e6a78e49506af0e238936e823b2f463e8087c20fcf4300232a'
            'c07276b69c7304981525ecb022f92daf7ae125a4fb05ac3442157b50826e257a')
md5sums=()
validpgpkeys=()

prepare() {
	mkdir -p brave
	bsdtar -xf "$pkgname-$pkgver.zip" -C brave
	chmod +x brave/brave
}

package() {
	install -dm0755 "$pkgdir/usr/lib"
	cp -a brave "$pkgdir/usr/lib/$pkgname"

	chmod 4755 "$pkgdir/usr/lib/brave-bin/chrome-sandbox"

	install -Dm0755 "$pkgname.sh" "$pkgdir/usr/bin/brave"
	install -Dm0644 -t "$pkgdir/usr/share/applications/" "brave-browser.desktop"
	install -Dm0644 -t "$pkgdir/usr/share/licences/$pkgname/" brave/LICENSE
	pushd "$pkgdir/usr/"
	for size in 16x16 24x24 32x32 48x48 64x64 128x128 256x256; do
		install -Dm0644 "lib/$pkgname/product_logo_${size/x*/}.png" \
			"share/icons/hicolor/$size/apps/brave-desktop.png"
	done
}


